#
# This file is part of Astarte.
#
# Copyright 2025 SECO Mind Srl
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

defmodule Astarte.Pairing.FDO.OwnerOnboarding.Done do
  @moduledoc """
  TO2.Done (Type 70).
  From Device ROE to Owner Onboarding Service.

  This message indicates successful completion of the Transfer of Ownership.
  The Device confirms it has processed the ServiceInfo and applied the configuration.

  Reference Section: 5.5.12 TO2.Done
  """
  use TypedStruct

  alias Astarte.Pairing.FDO.OwnerOnboarding.Done

  typedstruct enforce: true do
    @typedoc "Structure for TO2.Done message."

    # NonceTO2ProveDv
    # The Nonce originally generated by the Owner Onboarding Service
    # and sent to the Device in TO2.ProveOVHdr (Msg 60).
    # Its presence here confirms the session integrity and successful completion.
    field :nonce_to2_prove_dv, binary()
  end

  @doc """
  Decodes the raw CBOR binary into the Done struct.
  """
  @spec cbor_decode(binary()) :: {:ok, t()} | {:error, atom()}
  def cbor_decode(cbor_binary) do
    case CBOR.decode(cbor_binary) do
      {:ok, payload, _rest} -> decode(payload)
      _ -> {:error, :message_body_error}
    end
  end

  def decode(payload) do
    with [nonce] <- payload,
         %CBOR.Tag{tag: :bytes, value: nonce} <- nonce do
      {:ok, %Done{nonce_to2_prove_dv: nonce}}
    else
      _ -> {:error, :message_body_error}
    end
  end
end
